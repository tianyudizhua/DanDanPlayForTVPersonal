ext.mainApp = true  // 设置为true，表示此module为主app module，一直以application方式编译
apply from: rootProject.file('app-cc-setting.gradle')
apply from: rootProject.file('app-common-build.gradle')

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    throw new GradleException("Could not find key.properties!")
}

android {
    // VLC 需要
    dataBinding {
        enabled = true
    }
    // 签名配置信息
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    defaultConfig {
        applicationId Build.applicationId
        versionCode Build.versionCode
        versionName Build.versionName
    }
    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
            resValue("string", "PORT_NUMBER", "8080")
        }
        release {
            minifyEnabled true  // 混淆编译
            shrinkResources true // 混淆编译
            zipAlignEnabled true // zipalign优化
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    // 自定义输出apk名字
    applicationVariants.all {
        variant ->
            variant.outputs.all {
                if (variant.getBuildType().getName() == 'release') {
                    outputFileName = new File("app_v" + variant.versionName + "_" +
                            new Date().format("yyyy-MM-dd_HH", TimeZone.getTimeZone("UTC")) + "_" +
                            variant.getBuildType().getName() + ".apk")
                }
            }
    }
    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/armeabi-v8a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
    }
    splits {
        abi {
            enable = true
            reset()
            include("armeabi-v7a", "arm64-v8a", "x86", "x86_64")
            universalApk = true
        }
    }
}

dependencies {
    addComponent("module-player")
    addComponent("module-torrent")
    addComponent("module-tv")
    // Database Debug
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

//    // 内存泄漏
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'
}
